#!/bin/zsh
emulate -L zsh
setopt extended_glob

msg(){ print -- $* >&2 }
die(){ msg $*; exit 2 }
TRAPZERR(){
    msg 'BACKUP FAILED'
    [[ -e $stage ]] && msg 'Partial backup in '$stage
    exit 10
}
TRAPINT(){ TRAPZERR }
TRAPQUIT(){ TRAPZERR }
TRAPTERM(){ TRAPZERR }

: =btrfs =mktemp =rsync  =blkid =findmnt
[[ -z $HOST ]] && die 'Please set the hostname and try again'

device=$(blkid -l -o device -t LABEL=backup)
backups=$(findmnt -n -o target $device)/$HOST
stage=$backups/root

#last=$(print -- $backups/([0-9]##)/([0-9]##)/$HOST@*(/NnOn[1]))

final_path=$backups/$(print -P -- '%D{%Y/%m}/%M@%D{%Y%m%dT%H%M}')
#stage=$(mktemp -d --tmpdir=$backups)/$final_path:t

if [[ ! -d $stage ]]; then
	msg 'First backup of '$HOST' onto '$device' ('$backups')'
	btrfs subvolume create $stage
fi

rsync_log=$(mktemp --tmpdir=$stage:h)
# --checksum
rsync --log-file=$rsync_log -i --del --delete-excluded --filter='. /etc/backup' --archive --numeric-ids --hard-links --inplace --acls --xattrs --one-file-system --stats --omit-dir-times / $stage/

chmod 440 $rsync_log
chown root:wheel $rsync_log
mv -f $rsync_log $stage/backup.log

mkdir -p $final_path:h

btrfs subvolume snapshot -r $stage $final_path

msg 'Done'

